apply plugin: 'com.android.library'
apply plugin: 'checkstyle'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.neenbedankt.android-apt'

dependencies {
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'

    androidTestCompile 'com.squareup:fest-android:1.0.8@aar'
    androidTestCompile 'org.easytesting:fest-assert-core:2.0M10'
    androidTestCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'com.crittercism.dexmaker:dexmaker:1.4'
    androidTestCompile 'com.crittercism.dexmaker:dexmaker-dx:1.4'
    androidTestCompile 'com.crittercism.dexmaker:dexmaker-mockito:1.4'

    androidTestCompile('com.squareup.okhttp3:mockwebserver:3.3.0') {
        exclude group: 'com.squareup.okhttp3'
        exclude group: 'junit'
    }
    androidTestCompile('com.android.support:multidex:1.0.1')
    androidTestCompile('com.android.support:multidex-instrumentation:1.0.1') {
        exclude group: 'com.android.support', module: 'multidex'
    }
    androidTestApt 'com.google.dagger:dagger-compiler:2.4'
    androidTestCompile('com.google.dagger:dagger:2.4') {
        exclude group: 'javax.annotation'
    }

    androidTestCompile('com.squareup.retrofit2:retrofit-mock:2.1.0')

    compile 'net.danlew:android.joda:2.7.2'

    compile 'com.google.code.gson:gson:2.6.2'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.3.0'

    apt 'com.google.dagger:dagger-compiler:2.4'
    compile('com.google.dagger:dagger:2.4') {
        exclude group: 'javax.annotation'
    }
    provided 'javax.annotation:jsr250-api:1.0'

    apt "org.projectlombok:lombok:1.16.2"
    provided "org.projectlombok:lombok:1.16.2"
}

group = "com.sensorberg.sdk"
archivesBaseName = 'android-sdk'
version = "${project.ext.SDK_VERSION}${project.ext.RC_VERSION}"

def resolverURL = System.getenv("RESOLVER_URL") ?: project.ext.has("resolver_url") ? project.ext.get("resolver_url"): null

if (resolverURL != null) {
    println("using custom resolver: ${resolverURL}")
}

android {
    buildToolsVersion project.ext.buildToolsVersion
    compileSdkVersion project.ext.compileSdkVersion
    defaultConfig {
        versionCode 12
        versionName version
        minSdkVersion 9
        targetSdkVersion project.ext.targetSdkVersion
        buildConfigField "String", "SDK_VERSION", "\"" + project.ext.SDK_VERSION + "\""
        buildConfigField "String", "SDK_GIT_SHA1", gitSha1()
        buildConfigField "boolean", "NEW_BACKEND", "true"
        consumerProguardFiles 'proguard-proguarded.txt'
		testInstrumentationRunner "com.sensorberg.sdk.test.SensorbergTestRunner"
    }
    lintOptions {
        disable 'InvalidPackage', 'CommitPrefEdits', 'DefaultLocale', 'SameParameterValue'
        abortOnError false

    }
    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'LICENSE'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    buildTypes {
        debug {
            testCoverageEnabled true
            buildConfigField "String", "RESOLVER_URL", resolverURL ? wrapInQuotes(resolverURL) : "null"
        }
        release {
            testCoverageEnabled false
            versionNameSuffix "-RAILS"
        }
        releaseOld.initWith(release)
        releaseOld {
            buildConfigField "boolean", "NEW_BACKEND", "false"
            buildConfigField "String", "RESOLVER_URL", resolverURL ? wrapInQuotes(resolverURL) : "null"
            versionNameSuffix "-JAVA"
        }
    }
}

checkstyle {
    configFile project.file('checkstyle.xml')
    showViolations true
}

apply from: '../bintrayUpload.gradle'
apply from: '../snapshotUpload.gradle'

sonarqube {
    properties {
        /* SonarQube needs to be informed about your libraries and the android.jar to understand that methods like
         * onResume() is called by the Android framework. Without that information SonarQube will very likely create warnings
         * that those methods are never used and they should be removed. Same applies for libraries where parent classes
         * are required to understand how a class works and is used. */
        def libraries = project.android.sdkDirectory.getPath() + "/platforms/android-22/android.jar," +
                "build/intermediates/exploded-aar/**/classes.jar"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.sources", "src/main/java" // first defines where the java files are, the second where the xml files are
        property "sonar.binaries", "build/intermediates/classes/debug"
        property "sonar.java.binaries", "build/intermediates/classes/debug"
        property "sonar.libraries", libraries
        property "sonar.java.libraries", libraries
        property "sonar.jacoco.reportPath", "build/outputs/code-coverage/connected/coverage.ec"
    }
}

def wrapInQuotes(String wrapMe) {
    return "\"" + wrapMe + "\"";
}

def gitSha1() {
    final def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    final String output = stdout.toString().trim();
    return wrapInQuotes(output);
}